plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'com.unascribed.nilgradle' version '1.2.3'
}

sourceCompatibility = 11
targetCompatibility = 11

group = 'ssf.tf.sfort'
archivesBaseName = 'ocaip'
version = '1.0.1'

repositories {
	mavenCentral()
	maven {
		url 'https://repo.sleeping.town'
		content {
			includeGroup 'com.unascribed'
		}
	}
	maven {
		url 'https://maven.fabricmc.net'
		content {
			includeGroup 'net.fabricmc'
		}
	}
	maven {
		url 'https://maven.minecraftforge.net'
		content {
			includeGroup 'net.minecraftforge'
			includeGroup 'de.oceanlabs.mcp'
		}
	}
}

nil {
	id "ocaip"
	standalone true
}

configurations {
	shade
	compileOnly.extendsFrom shade
}

dependencies {
	loader 'com.unascribed:nilloader:1.1.3'

	game nil.minecraft('1.18.2', 'client')
	implementation 'io.netty:netty-buffer:4.1.77.Final'
	shade 'net.i2p.crypto:eddsa:0.3.0'
}

shadowJar {
	configurations = [project.configurations.shade]
	minimize()
}

mappings {
	work 'net.fabricmc:yarn:1.18.2+build.3:v2'
	via 'net.fabricmc:intermediary:1.18.2:v2'
	target 'net.fabricmc:intermediary:1.18.2:v2'
	target nil.mojmap('1.18.2', 'client')
	target 'de.oceanlabs.mcp:mcp_config:1.18.2'
}

tasks.build.dependsOn tasks.shadowJar
